///
// THIS PROGRAM IS ADAPTED FROM Eigen_Bridge_Potential.edp from LiquidBridges++_V1.3
// imported in STABFEM project by D. Fabre on december 2017.
// STILL TO BE RATIONALIZED A LOT !!!


//  Computation of the eigenvalues and eigenvectors for oscillations of a liquid bridge
//  (inviscid theory)
// ----------------------------------------
// the problem is potential : u = i grad (phi) (mind the "i" factor !) 
// PROBLEM : find $\omega$ and $[phi,eta]$  such that
// 	$$ \Delta \phi = 0 $$ inside the domain
// 	$$ \omega \phi = \gamma (-(K0a^2+K0b^2) \eta - \partial^2 \eta / \partial s^2 + m^2/r^2 \eta $$ on the free surface
// 	$$ \omega \eta = grad \phi \dot n $$ on the free surface 
// 
//-----------------------------------------

// Remarque : phi et eta sont definis comme des champs P2 sur le volume, meme si eta n'a de sens qu'à la surface

include "SF_Geom.edp";
include "Macros_StabFem.edp"


verbosity=0;

// constantes physiques 
//real g=0;                 // Gravity field
real gamma=1;	  // Surface Tension
real R=1;
real dpdz;
real GAMMA;

cout << "ENTREZ LA TENSION DE SURFACE gamma : "<< endl;
cin >> gamma;



cout << "ENTREZ LE PARAMETRE DE GRAVITE dp/dz = rho g : "<< endl;
cin >> dpdz;

//cout << "ENTREZ LA CIRCULATION GAMMA : "<< endl;
//cin >> GAMMA;
GAMMA = SFGeomGAMMA; // PARAMETER PICKED FROM "SF_Geom.edp"
cout << "GAMMA = " << GAMMA << endl;

// Parametres du calcul
int m;  // Nombre d'onde

cout << " ENTREZ Le nombre d'onde m ?" << endl;
cin >> m;
cout << "==> Calcul des valeurs propres pour m = " << m << endl;

int nev;
cout << " ENTREZ le nombre de modes a calculer " << endl;
cin >> nev;
int Nval = nev;  // nb. de valeurs propres à calculer
int Nmodes = nev; // nb. de vecteurs propres à post-traiter


real shift;
cout << " ENTREZ le shift (here only imaginary part) : " << endl;
cin >> shift;






////////////////////////////////////////////////////////////////
//
// MAILLAGE ET GEOMETRIE INITIAUX
//
////////////////////////////////////////////////////////////////


mesh th=readmesh(ffdatadir+"mesh.msh");
//mesh th=th; // autre nom possible...
mesh Shempty=emptymesh(th); 	// Maillage 'surface'
real L, K0, Vol, Area;

include "Macros_Geometrie_Surface.edp"

SurfaceNumbering(IndexPointSurf,xPointSurf,yPointSurf,S0PointSurf,Nsurface); // cf. macro
// Calcul des normales et des courbures
CalcGeomSurf;




fespace VhxVh(th,[P1,P1]);

VhxVh [phi,eta],[q,v];


real RegEta = 1e-8;


// MACROS

macro DPDR(r) (GAMMA/2/pi)^2*(1/r^3) //EOM

// CONSTRUCTION DES MATRICES

varf a([phi,eta],[q,v]) = // order of test fun. shoud be [v,q] for SLEPC ; [q,v] instead
// Laplacian	  
	  int2d(th)(  (dx(phi)*dx(v) + dy(phi)*dy(v)+(m^2+1e-8)*phi*v/(x^2+1e-30))*x  ) 
	  		// desingularisation term necessary if m=0 ; don't know why
// Curvature	  
	+ int1d(th,2)(gamma*((Ds(eta))*(Ds(q))  -(K0a^2+K0b^2)*eta*q+m^2/(x^2+1e-30)*q*eta)*x)
// gravity and centrifugal force
	+int1d(th,2)(q*eta*(dpdz*N.y-DPDR(x)*N.x)*x)
// convective terms if rotation is present
 	+ int1d(th,2)(m*GAMMA/(2*pi*x^2)*(eta*v+q*phi)*x)
// shift 
	- int1d(th,2)(shift*(eta*v+q*phi)*x)
// Pour regulariser eta dans le volume	
		+ int2d(th)((dx(q)*dx(eta)+dy(q)*dy(eta))*RegEta*x) //  deuxieme idee : laplacien 
// Conditions limites par penalisation
+ int1d(th,1)(1e30*eta*q*abs(x)) // pour ligne de contact fixee
+ int1d(th,5)(1e30*eta*q*abs(x)*gamma) // pour ligne de contact fixee si gamma ne 0.
+ int1d(th,1)(m*phi*v*1e30) // CL axe pour m different de 0
;


varf b([phi,eta],[q,v]) = 
        int1d(th,2)(eta*v*x)
       +int1d(th,2)(q*phi*x)
		      ;


////////////////////////////////////////////////////
//
//  RESOLUTION DU PB AUX VALEURS PROPRES
//
//  Important warning : here the computed eigenvalues correspond to omega = lambda/i !
//  
/////////////////////////////////////////////////////


matrix A= a(VhxVh,VhxVh,solver=UMFPACK); 
matrix B= b(VhxVh,VhxVh,solver=GMRES,eps=1e-20); 

//int nev=Nval;  // number of computed eigen valeu close to sigma
real[int] evr(Nval+1),evi(Nval+1); // to store nev eigein values
VhxVh[int] [eVphi,eVeta](Nval+1);   // to store nev eigen vectors

int k=EigenValue(A,B,sym=false,sigma=shift,value=evr,ivalue=evi,vector=eVphi,tol=1e-10,maxit=0,ncv=0);



////////////////////////////////////////////////////
//
//    POST TRAITEMENT
//
///////////////////////////////////////////////////


cout << " ### VP Calculees :" << endl;

//real[int]  eev(Nval);
ofstream EV(ffdatadir+"Spectrum.txt");
EV.scientific;

VhxVh<complex> [Modephi,Modeeta];
complex lambda;

for (int i=0;i<Nval;i++)
{
if(evi[i] == 0)
	{// real eigenvalue -> purely oscillating mode
	lambda = 1i*evr[i];
	[Modephi,Modeeta] = [1i*eVphi[i],(1.+0i)*eVeta[i]];
	}
else if(i>0&&abs(evi[i])==abs(evi[abs(i-1)])) // abs is to prevent a bug 
	{// pair of complex eigenvalues with previous one
	lambda = 1i*evr[i]+evi[i-1];
	[Modephi,Modeeta] = [1i*eVphi[i]+eVphi[i-1],eVeta[i]-1i*eVeta[i-1]];
	}
else if(i<Nval&&abs(evi[i])==abs(evi[i+1])) 
	{// pair of complex eigenvalues with next one
	lambda = 1i*evr[i]+evi[i+1];
	[Modephi,Modeeta] = [1i*eVphi[i]-eVphi[i+1],eVeta[i]-1i*eVeta[i+1]];
	};


{
  cout << " VP numero " <<  i<< " : " << real(lambda) << "+i"<<imag(lambda) <<  endl;
  EV << -evi[i] << "  "<< evr[i] << " " << L << "  " << K0  << " " << Vol << "  " << dpdz << "   " << m  <<  endl;
}; 
  
    
// Post-traitement MODES
if (i<Nmodes)
   {	
   	{
   	ofstream FileTXT(ffdatadir+"Eigenmode"+(i+1)+".txt");
   	FileTXT << eVphi[i][];
   	FileTXT << endl << endl << lambda << endl;
   	}
   SFWriteModePotential(ffdatadir+"Eigenmode"+(i+1)+".ff2m",Mode,lambda);
   IFMACRO(FREEFEM,YES)
   plot(eVphi[i],cmm="Mode "+i+" ; omega = "+evi[i]+" ; sigma = "+evr[i],wait=1,fill=1);
   ENDIFMACRO    	
   };
}	
	
	// HACK for compatibility
	if(nev==1){ 
		exec("cp "+ffdatadir+"Eigenmode0.txt "+ffdatadir+"Eigenmode.txt");
		exec("cp "+ffdatadir+"Eigenmode0.ff2m "+ffdatadir+"Eigenmode.ff2m");
		};


cout << " ### StabAxi_FreeSurface_Potenital.edp correctly ended." << endl;


